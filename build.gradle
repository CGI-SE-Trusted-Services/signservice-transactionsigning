plugins {
    id("groovy")
    id("java-library")
    id("com.gradleup.shadow") version "9.2.2"
}

group = "se.signatureservice.support"

pluginManager.withPlugin('org.certificateservices.gradle.certservice-gradle-plugin') {
    certserviceSettings {
        licenseName = "GNU Lesser General Public License v3.0 or later"
        licenseURL = "https://www.gnu.org/licenses/lgpl-3.0-standalone.html"
        useDeveloperPOM = true
        useScmPOM = true
        scmConnection = "scm:git:https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning.git"
        scmDeveloperConnection = "scm:git:https://git@github.com/CGI-SE-Trusted-Services/signservice-transactionsigning.git"
        scmUrl = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning"
        projectUrl = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "eu.europa.ec.joinup.sd-dss:dss-xades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-cades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-pades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-service:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-utils:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-utils-apache-commons:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-crl-parser-x509crl:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-validation:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-pades-pdfbox:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-cms-object:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-tsl-validation:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-diagnostic-jaxb:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-policy-jaxb:${dssVersion}"
    implementation "org.slf4j:slf4j-api:2.0.17"
    implementation "org.slf4j:slf4j-simple:2.0.17"
    implementation "org.json:json:20250517"
    implementation "org.apache.groovy:groovy:${groovyVersion}"

    // To fix vulnerability CVE-2025-27820 derived from eu.europa.ec.joinup.sd-dss:dss-service
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.5'
    implementation 'org.apache.httpcomponents.core5:httpcore5:5.3.4'
    implementation 'org.apache.httpcomponents.core5:httpcore5-h2:5.3.4'

    // To fix vulnerability CVE-2025-48924
    implementation 'org.apache.commons:commons-lang3:3.20.0'

    testImplementation platform("org.spockframework:spock-bom:$spokVersion")
    testImplementation "org.spockframework:spock-core"
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName = 'signservice-transactionsigning'
    mergeServiceFiles()
    archiveClassifier = ''
    //exclude 'META-INF/versions/21/**'
    dependencies {
        exclude(dependency("org.slf4j:slf4j-simple:2.0.17"))
    }
    relocate 'org.apache', 'signservice.org.apache'
    relocate 'org.json', 'signservice.org.json'
    relocate 'com.ctc', 'signservice.com.ctc'
    relocate 'org.bouncycastle', 'signservice.org.bouncycastle'
    relocate 'org.codehaus', 'signservice.org.codehaus'
}

tasks.build.dependsOn tasks.shadowJar
if(tasks.findByName("zipBin") && tasks.findByName("shadowJar")) {
    tasks.zipBin.dependsOn tasks.shadowJar
}
