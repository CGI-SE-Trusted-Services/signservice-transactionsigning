plugins {
    id("groovy")
    id("java-library")
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

group "se.signatureservice.support"

pluginManager.withPlugin('org.certificateservices.gradle.certservice-gradle-plugin') {
    certserviceSettings {
        useMavenCentral = true
        licenseName = "GNU Lesser General Public License v3.0 or later"
        licenseURL = "https://www.gnu.org/licenses/lgpl-3.0-standalone.html"
        useDeveloperPOM = true
        useScmPOM = true
        scmConnection = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning.git"
        scmDeveloperConnection = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning.git"
        scmUrl = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning"
        projectUrl = "https://github.com/CGI-SE-Trusted-Services/signservice-transactionsigning"
    }
}

repositories {
    mavenCentral()
}

configurations.configureEach {
    resolutionStrategy {
        eachDependency { details ->
            // 1.77 is the highest version of bc jars that is tolerated
            // by the current shadowJar plugin, since later version
            // contain classes compiled with java 21.
            // Upgrade of shadowJar plugin requires higher versions of gradle
            if (details.requested.group == 'org.bouncycastle') {
                details.useVersion '1.77'
            }
        }
    }
}

dependencies {
    implementation "eu.europa.ec.joinup.sd-dss:dss-xades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-cades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-pades:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-service:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-utils:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-utils-apache-commons:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-crl-parser-x509crl:${dssVersion}"
    implementation "eu.europa.ec.joinup.sd-dss:dss-validation:${dssVersion}"
    implementation "org.slf4j:slf4j-api:2.0.16"
    implementation "org.slf4j:slf4j-simple:2.0.16"
    implementation "org.json:json:20250107"
    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName = 'signservice-transactionsigning'
    mergeServiceFiles()
    archiveClassifier = ''
    dependencies {
        exclude(dependency("org.slf4j:slf4j-simple:2.0.16"))
    }
    relocate 'org.apache', 'signservice.org.apache'
    relocate 'org.json', 'signservice.org.json'
    relocate 'com.ctc', 'signservice.com.ctc'
    relocate 'org.bouncycastle', 'signservice.org.bouncycastle'
    relocate 'org.codehaus', 'signservice.org.codehaus'
}

tasks.build.dependsOn tasks.shadowJar
if(tasks.findByName("zipBin") && tasks.findByName("shadowJar")) {
    tasks.zipBin.dependsOn tasks.shadowJar
}
